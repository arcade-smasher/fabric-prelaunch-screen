plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
}

// build with specific version, like `./gradlew build -PmcVersion=1.20.5`
def mcVersion = project.findProperty("mcVersion") ?: "1.20.5"
println "Building for Minecraft ${mcVersion}"

ext {
	versionSourceDir = "src/v${mcVersion.replace('.', '_')}"
}

dependencies {
	minecraft "com.mojang:minecraft:${mcVersion}"

	// pick correct Yarn mapping based on mcVersion
	def mappingProp = "yarn_${mcVersion.replace('.', '_')}_mappings"
	if (!project.hasProperty(mappingProp)) {
		throw new GradleException("Missing mapping version for ${mcVersion}. Define ${mappingProp} in gradle.properties.")
	}
	mappings "net.fabricmc:yarn:${project[mappingProp]}:v2"

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

sourceSets {
	main {
		java {
			srcDir "${versionSourceDir}/java"
		}
		resources {
			srcDir "${versionSourceDir}/resources"
		}
	}
}

processResources {
	inputs.property "version", project.version
	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 8
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

jar {
    archiveBaseName.set("${project.archives_base_name}_mc${mcVersion}")
    archiveVersion.set(project.version)
    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}" }
    }
}

tasks.named("remapJar") {
    archiveBaseName.set("${project.archives_base_name}")
    archiveVersion.set(project.version)
    archiveClassifier.set(mcVersion) // appends Minecraft version
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}
}
